apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

project.archivesBaseName = "$rootProject.artifactPrefix-library"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name "$rootProject.projectName Library"
                packaging 'jar'
                description 'Annotations Library for Resource Provider, an MVP Resources API for Android'
                artifactId "$rootProject.artifactPrefix-library"
                url 'https://github.com/Comcast/resourceprovider'

                scm {
                    url 'scm:git@github.com:Comcast/resourceprovider.git'
                    connection 'scm:git@github.com:Comcast/resourceprovider.git'
                    developerConnection 'scm:git@github.com:Comcast/resourceprovider.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dapp'
                        name 'Mark Dappollone'
                    }
                }
            }
        }
    }
}